///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО Фобизнес ПРО
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Функция - Определить все возможные свойства и настройки методов заложенные в API протокола WITSML
// Из полученной таблицы будут отбираться доступные для нашего сервиса
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Содержит все методы их свойства и настройки
//
Функция ОпределитьНастройкиМетодов() Экспорт
	
	тзНастройкиДоступныхПолейМетодов = Новый ТаблицаЗначений;
	
	Макет = ПолучитьОбщийМакет("РаботаСGTIOnlineМакет");
	
	Если Макет.Области.Количество() = 0 Тогда
		
		ЗаписьЖурналаРегистрации(НСтр("ru='Не найден макет заполнения для работы с GTI-Online.'"), 
		УровеньЖурналаРегистрации.Ошибка,
		Метаданные.ОбщиеМакеты.РаботаСGTIOnlineМакет);
		
		Возврат тзНастройкиДоступныхПолейМетодов;
		
	КонецЕсли;
	
	ПостроительОтчета = Новый ПостроительОтчета;                                    
	ПостроительОтчета.ИсточникДанных = Новый ОписаниеИсточникаДанных(Макет.Области[0]);
	ПостроительОтчета.Выполнить();
	тзНастройкиДоступныхПолейМетодов = ПостроительОтчета.Результат.Выгрузить();
	
	Возврат тзНастройкиДоступныхПолейМетодов;
	
КонецФункции

// Функция - Определить настройки запроса к методу по шаблону
//
// Параметры:
//  СтруктураПолученияЗапроса	 - Структура - Параметры запроса и тип запроса для сервиса.
// 
// Возвращаемое значение:
//  Структура - Параметры заполнения страницы формы "Настройка"
//    * WMLtypeIn - Строка - Тип запроса для сервиса.
//    * QueryIn   - Строка - Текст запроса для исполнения сервисом.
//
Функция ОпределитьНастройкиЗапросаКМетоду_ПоШаблону(СтруктураПолученияЗапроса) Экспорт
	
	СтруктураОтвета = Новый Структура("WMLtypeIn, QueryIn");
		
	Макет = ПолучитьОбщийМакет("РаботаСGTIOnline_ШаблоныЗапросов");
	
	НайденнаяОбласть = Макет.Области.Найти(СтруктураПолученияЗапроса.Значение_WMLtypeIn); 
	Если НайденнаяОбласть = Неопределено Тогда 
		ЗаписьЖурналаРегистрации(НСтр("ru='Не найден макет с шаблонами запросов для работы с GTI-Online.'"), 
		УровеньЖурналаРегистрации.Ошибка,
		Метаданные.ОбщиеМакеты.РаботаСGTIOnline_ШаблоныЗапросов);	
		
	Иначе
		
		НомерСпецСимволаВСтроке = СтрНайти(СтруктураПолученияЗапроса.Значение_WMLtypeIn, "_");
		Если НомерСпецСимволаВСтроке = 0 Тогда 
			СтруктураОтвета.Вставить("WMLtypeIn", СтруктураПолученияЗапроса.Значение_WMLtypeIn);
		Иначе 	
			СтруктураОтвета.Вставить("АльтернативнаяТаблицаДляВыводаНаФорму", "log_Data");
			ТипЗапроса = Лев(СтруктураПолученияЗапроса.Значение_WMLtypeIn, НомерСпецСимволаВСтроке-1);
			СтруктураОтвета.Вставить("WMLtypeIn", ТипЗапроса);
			
		КонецЕсли;	
		
		// TODO Сделать обход
		ТекстЗапроса = НайденнаяОбласть.Текст;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%uidLog%", СтруктураПолученияЗапроса.uidLog);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%uidWell%", СтруктураПолученияЗапроса.uidWell); 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%uidWellbore%", СтруктураПолученияЗапроса.uidWellbore);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%indexType%", СтруктураПолученияЗапроса.indexType);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%indexCurve%", СтруктураПолученияЗапроса.indexCurve);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%startDateTimeIndex%", СтруктураПолученияЗапроса.startDateTimeIndex);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%endDateTimeIndex%", СтруктураПолученияЗапроса.endDateTimeIndex);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%uidLogCurveInfo%", СтруктураПолученияЗапроса.uidLogCurveInfo);	
		
		СтруктураОтвета.Вставить("QueryIn", ТекстЗапроса);
		
	КонецЕсли;
	
	Возврат СтруктураОтвета;	
	
КонецФункции	

#КонецОбласти
